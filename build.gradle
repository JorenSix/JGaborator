import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

sourceCompatibility=11
targetCompatibility=11

ext.moduleName="jgaborator"
group 'be.ugent.jgaborator'
version '0.7'

repositories {
    maven {
        name = "0110 repository"
        url = "https://mvn.0110.be/releases"
    }
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                "Main-Class": "be.ugent.jgaborator.ui.JGaboratorBrowser",
                "Build-Version": project.version,
                "Build-By" : System.properties['user.name'],
                "Build-Date":  new SimpleDateFormat("yyyy-MM-dd").format(new Date()),
                'Build-JDK' : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'  : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

dependencies {
    implementation 'be.tarsos.dsp:core:2.5'
    implementation 'be.tarsos.dsp:jvm:2.5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId
            pom {
                name = 'JGaborator'
                description = 'Fine-grained Spectral transforms in Java'
                url = 'https://github.com/JorenSix/JGaborator'
                licenses {
                    license {
                        name = 'AGPL'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'JorenSix'
                        name = 'Joren Six'
                        email = 'joren.six@ugent.be'
                    }
                }
                scm {
                    connection = 'https://github.com/JorenSix/JGaborator.git'
                    developerConnection = 'git@github.com:JorenSix/JGaborator.git'
                    url = 'https://github.com/JorenSix/JGaborator'
                }
            }
        }
    }

    repositories {
        //Github packages repository. Unfortunately authentication is also needed for public
        //packages, which limits the use of it altogether.
        //It is, however, a practical backup...
        //https://github.com/orgs/community/discussions/26634
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/JorenSix/JGaborator"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }

        //public, self hosted repository
        maven {
            name = "reposilite"
            url = "https://mvn.0110.be/releases"
            credentials {
                username = System.getenv("REPOSILITE_ACTOR")
                password = System.getenv("REPOSILITE_TOKEN")
            }
        }
    }
}